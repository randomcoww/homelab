
locals {
  mcp_port      = 8080
  service_port  = 8081
  oauth_db_file = "/var/run/mcp-oauth-proxy/mcp-auth.db"
  oauth_config = merge({
    EXTERNAL_URL            = "https://${var.ingress_hostname}"
    NO_AUTO_TLS             = true
    TLS_ACCEPT_TOS          = true
    NO_PROVIDER_AUTO_SELECT = true
    LISTEN                  = "0.0.0.0:${local.service_port}"
    DATA_PATH               = "/data"
    REPOSITORY_BACKEND      = "sqlite"
    REPOSITORY_DSN = "file:${local.oauth_db_file}?${join("&", [
      "cache=shared",
      "mode=rwc",
      "_busy_timeout=5000",
      "_journal_mode=WAL",
      "_foreign_keys=on",
      "synchronous=normal",
    ])}"
  }, var.extra_oauth_configs)

  oauth_private_key_file = "${local.oauth_config["DATA_PATH"]}/private_key.pem" # hardcoded filename
  oauth_secret_file      = "${local.oauth_config["DATA_PATH"]}/secret"          # hardcoded filename
}

# This is normally generated by mcp-oauth-proxy. Pregenerate this how it is internally created.
# Needs private_key_pem_pkcs8 format
resource "tls_private_key" "oauth" {
  algorithm = "RSA"
  rsa_bits  = 2048
}

# This is normally generated by mcp-oauth-proxy. Pregenerate this how it is internally created.
resource "random_password" "oauth" {
  length           = 32
  override_special = "-_"
}

module "metadata" {
  source      = "../../../modules/metadata"
  name        = var.name
  namespace   = var.namespace
  release     = var.release
  app_version = var.release
  manifests = merge({
    "templates/statefulset.yaml" = module.statefulset.manifest
    "templates/secret.yaml"      = module.secret.manifest
    "templates/service.yaml"     = module.service.manifest
    "templates/ingress.yaml"     = module.ingress.manifest
    }, {
    for i, m in module.litestream-overlay.additional_manifests :
    "templates/litestream-${i}.yaml" => m
  })
}

module "secret" {
  source  = "../../../modules/secret"
  name    = var.name
  app     = var.name
  release = var.release
  data = merge({
    basename(local.oauth_private_key_file) = tls_private_key.oauth.private_key_pem_pkcs8
    basename(local.oauth_secret_file)      = random_password.oauth.result
    }, {
    for k, v in local.oauth_config :
    tostring(k) => tostring(v)
  })
}

module "service" {
  source  = "../../../modules/service"
  name    = var.name
  app     = var.name
  release = var.release
  spec = {
    type = "ClusterIP"
    ports = [
      {
        name       = "mcp"
        port       = local.service_port
        protocol   = "TCP"
        targetPort = local.service_port
      },
    ]
  }
}

module "ingress" {
  source             = "../../../modules/ingress"
  name               = var.name
  app                = var.name
  release            = var.release
  ingress_class_name = var.ingress_class_name
  annotations        = var.nginx_ingress_annotations
  rules = [
    {
      host = var.ingress_hostname
      paths = [
        {
          service = module.service.name
          port    = local.service_port
          path    = "/"
        },
      ]
    },
  ]
}

module "litestream-overlay" {
  source = "../litestream_overlay"

  name    = var.name
  app     = var.name
  release = var.release
  images = {
    litestream = var.images.litestream
  }
  litestream_config = {
    dbs = [
      {
        path                = local.oauth_db_file
        monitor-interval    = "1s"
        checkpoint-interval = "60s"
        replica = {
          type          = "s3"
          endpoint      = var.minio_endpoint
          bucket        = var.minio_bucket
          path          = "$POD_NAME/litestream"
          sync-interval = "1s"
        }
      },
    ]
  }
  sqlite_path         = local.oauth_db_file
  minio_access_secret = var.minio_access_secret

  template_spec = {
    resources = {
      requests = {
        memory = "256Mi"
      }
      limits = {
        memory = "256Mi"
      }
    }
    containers = [
      {
        name  = var.name
        image = var.images.prometheus_mcp
        ports = [
          {
            containerPort = local.mcp_port
          },
        ]
        env = [
          {
            name  = "SSL_CERT_FILE"
            value = "/etc/ssl/certs/ca-certificates.crt"
          },
          {
            name  = "REQUESTS_CA_BUNDLE"
            value = "/etc/ssl/certs/ca-certificates.crt"
          },
          {
            name  = "PROMETHEUS_URL"
            value = var.prometheus_url
          },
          {
            name  = "PROMETHEUS_URL_SSL_VERIFY"
            value = "true"
          },
          {
            name  = "PROMETHEUS_MCP_SERVER_TRANSPORT"
            value = "http"
          },
          {
            name  = "PROMETHEUS_MCP_BIND_HOST"
            value = "0.0.0.0"
          },
          {
            name  = "PROMETHEUS_MCP_BIND_PORT"
            value = tostring(local.mcp_port)
          },
        ]
        volumeMounts = [
          {
            name      = "ca-trust-bundle"
            mountPath = "/etc/ssl/certs/ca-certificates.crt"
            readOnly  = true
          },
        ]
        # TODO: add health checks
      },
      {
        name  = "${var.name}-oauth-proxy"
        image = var.images.mcp_oauth_proxy
        args = [
          "http://127.0.0.1:${local.mcp_port}",
        ]
        ports = [
          {
            containerPort = local.service_port
          },
        ]
        env = [
          for k, v in local.oauth_config :
          {
            name = tostring(k)
            valueFrom = {
              secretKeyRef = {
                name = module.secret.name
                key  = tostring(k)
              }
            }
          }
        ]
        volumeMounts = [
          {
            name      = "oauth-data"
            mountPath = local.oauth_config["DATA_PATH"]
          },
          {
            name      = "config"
            mountPath = local.oauth_private_key_file
            subPath   = basename(local.oauth_private_key_file)
          },
          {
            name      = "config"
            mountPath = local.oauth_secret_file
            subPath   = basename(local.oauth_secret_file)
          },
        ]
        # TODO: add health checks
      },
    ]
    volumes = [
      {
        name = "ca-trust-bundle"
        hostPath = {
          path = "/etc/ssl/certs/ca-certificates.crt"
          type = "File"
        }
      },
      {
        name = "${var.name}-litestream-data"
        emptyDir = {
          medium = "Memory"
        }
      },
      {
        name = "config"
        secret = {
          secretName = module.secret.name
        }
      },
      {
        name = "oauth-data"
        emptyDir = {
          medium = "Memory"
        }
      },
    ]
  }
}

module "statefulset" {
  source = "../../../modules/statefulset"

  name     = var.name
  app      = var.name
  release  = var.release
  affinity = var.affinity
  replicas = var.replicas
  annotations = merge({
    "checksum/secret" = sha256(module.secret.manifest)
    }, {
    for i, m in module.litestream-overlay.additional_manifests :
    "checksum/litestream-${i}" => sha256(m)
  })

  /* persistent path for sqlite
  spec = {
    volumeClaimTemplates = [
      {
        metadata = {
          name = "${var.name}-litestream-data"
        }
        spec = {
          accessModes = [
            "ReadWriteOnce",
          ]
          resources = {
            requests = {
              storage = "16Gi"
            }
          }
          storageClassName = "local-path"
        }
      },
    ]
  }
  */
  template_spec = module.litestream-overlay.template_spec
}